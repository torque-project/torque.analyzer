;; -*- mode: clojure;-*-
;;   Copyright (c) Nicola Mometto, Rich Hickey & contributors.
;;   The use and distribution terms for this software are covered by the
;;   Eclipse Public License 1.0 (http://opensource.org/licenses/eclipse-1.0.php)
;;   which can be found in the file epl-v10.html at the root of this distribution.
;;   By using this software in any fashion, you are agreeing to be bound by
;;   the terms of this license.
;;   You must not remove this notice, or any other, from this software.

(ns torque.analyzer.env)

(def ^:dynamic *env*
  nil)

(defn ^:private throw-not-an-env [x]
  (throw
    (ex-info
      (str "global env must be a map or atom containing a map, not "
           (type x))
      {:env x})))

(defn ^:private env? [x]
  (and (instance? torque.lang.atom/Atom x)
       (map? @x)))

(defmacro with-env
  [env & body]
  `(let [env# ~env
         env# (cond
                (map? env#) (atom env#)
                (env? env#) env#
                :default    (throw-not-an-env env#))]
     (binding [*env* env#]
       ~@body)))

;; if *env* is not bound, bind it to env
(defmacro ensure
  [env & body]
  `(if *env*
     (do ~@body)
     (with-env ~env
       ~@body)))

(defn deref-env
  []
  (if *env*
    @*env*
    (throw (ex-info "Global env not bound" {}))))
